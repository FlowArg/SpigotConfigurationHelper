plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

version = '1.0.1'
group = 'fr.flowarg'
archivesBaseName = 'sch'

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

repositories {
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/public"
    }
    jcenter()
    mavenCentral()
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : archivesBaseName,
                "Specification-Vendor"    : "FlowArg",
                "Specification-Version"   : project.version,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "flowarg",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Built-By'                : System.properties['user.name'],
                'Created-By'              : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'               : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'                : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Automatic-Module-Name'   : 'sch'
        ])
    }
}

artifacts {
    archives sourcesJar, javadocJar
}
        
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            
            pom {
                groupId = project.group
                version = project.version
                artifactId = 'sch'
                name = project.name
                description = 'A Spigot library for manage configurations.'
                url = 'https://github.com/FlowArg/SpigotConfigurationHelper'

                scm {
                    connection = 'scm:git:git://github.com/FlowArg/SpigotConfigurationHelper.git'
                    developerConnection = 'scm:git:ssh://github.com:FlowArg/SpigotConfigurationHelper.git'
                    url = 'http://github.com/FlowArg/SpigotConfigurationHelper/tree/master'
                }

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'flowarg'
                        name = 'Flow Arg'
                        email = 'coutureflorence3@gmail.com'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
